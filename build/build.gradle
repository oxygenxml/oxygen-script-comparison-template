task initScript() {
    doFirst {
        if (!new File("${projectDir}/extracted/oxygen/").exists()) {
            ant.get(src: 'https://www.oxygenxml.com/InstData/Editor/All/oxygen.tar.gz', dest: "${projectDir}/oxygen.tar.gz", skipexisting: 'true')
            ant.untar(src: "${projectDir}/oxygen.tar.gz", dest: "${projectDir}/extracted", compression: 'gzip')

            String key = System.getenv('SCRIPTING_LICENSE_KEY')
            if (key == null) {
                throw new TaskExecutionException(it, new Exception("You need a valid Oxygen Scripting license key."))
            } else {
                new File("${projectDir}/extracted/oxygen/scriptinglicensekey.txt").write(key.replaceAll("  ", "\n"))
            }
        }
    }
    doLast {
        project.exec {
            workingDir "${projectDir}/extracted/oxygen/scripts"
            commandLine 'chmod', '700', 'compareDirs.sh'
        }
    }
}

task runComparison(type: Exec, dependsOn: [initScript]) {
    // Compute the branch directories depending on where the script is started from.
    String firstDir = "${projectDir}" + (innerBuild.toBoolean() ? "/../" : "/../../") + firstBranch
    String secondDir = "${projectDir}" + (innerBuild.toBoolean() ? "/../" : "/../../") + secondBranch
    String baseDir = "${projectDir}" + (innerBuild.toBoolean() ? "/../" : "/../../") + "base"

    workingDir "${projectDir}/extracted/oxygen"
    if (new File(baseDir).exists()) {
        commandLine "scripts/compareDirs.sh",
                firstDir, secondDir, baseDir,
                "-ed", ".git", // ignore .git directory
                "-out", "html",
                "-outfile", "${projectDir}/../report/comparisonReport.html"
    } else {
        commandLine "scripts/compareDirs.sh",
                firstDir, secondDir,
                "-ed", ".git", // ignore .git directory
                "-out", "html",
                "-outfile", "${projectDir}/../report/comparisonReport.html"
    }

    doLast {
        if (execResult.getExitValue() != 0) {
            throw new TaskExecutionException(it, new Exception("Comparison failed, see task output for details."))
        } else {
            // Replace file locations from GitHub VM with locations from repository.
            ant.replace(
                    file: '../report/comparisonReport.html',
                    token: 'file:/home/runner/work/oxygen-script-comparison-template/oxygen-script-comparison-template/main',
                    value:  repoMainBranchUrl
            )
            ant.replace(
                    file: '../report/comparisonReport.html',
                    token: '/home/runner/work/' + repoName,
                    value: ''
            )
        }        
    }
}

defaultTasks 'runComparison'

